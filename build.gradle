import java.util.regex.Pattern

plugins {
    id "fabric-loom" version "1.4-SNAPSHOT"
    id "com.matthewprenger.cursegradle" version "1.4.0"
    id "com.modrinth.minotaur" version "2.+"
}

version = project.mod_version + "-fabric-" + project.minecraft_suffix
group = project.maven_group
archivesBaseName = project.mod_id

sourceCompatibility = JavaLanguageVersion.of(project.java_target)
targetCompatibility = JavaLanguageVersion.of(project.java_target)

println("Java: " + System.getProperty("java.version") + " JVM: " + System.getProperty("java.vm.version") + "(" + System.getProperty("java.vendor") + ") Arch: " + System.getProperty("os.arch"))

repositories {
    flatDir {
        dirs "libs"
    }
    exclusiveContent {
        forRepository {
            maven {
                name = "CurseForge"
                url = "https://www.cursemaven.com"
            }
        }
        filter {
            includeGroup "curse.maven"
        }
    }
    exclusiveContent {
        forRepository {
            maven {
                name = "Modrinth"
                url = "https://api.modrinth.com/maven"
            }
        }
        filter {
            includeGroup "maven.modrinth"
        }
    }
}

dependencies {
    // Minecraft
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    // Official mappings
    mappings loom.officialMojangMappings()

    // Fabric Loader
    modImplementation "net.fabricmc:fabric-loader:${project.fabric_loader_version}"
    // Fabric API
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_api_version}"

    // Just Enough Items
    modRuntimeOnly "curse.maven:jei-238222:${project.just_enough_items_file}"
}

// Include resources generated by data generators.
sourceSets.main.resources { srcDir "src/generated/resources" }

processResources {
    inputs.property "version", project.version

    // Replace the mod version
    Map<String, ?> copyProperties = project.properties.clone() as Map<String, ?>
    var matcher = Pattern.compile("[^.0-9]").matcher(copyProperties.mod_version as String)
    if (matcher.find())
        copyProperties.mod_version = copyProperties.mod_version.substring(0, matcher.start()) + "+" + copyProperties.mod_version.substring(matcher.start())

    filesMatching(["fabric.mod.json", "modid.mixins.json", "pack.mcmeta"]) {
        expand copyProperties
    }

    exclude "**/*.pdn"

    rename "^icon.png\$", "${project.mod_id}.png"
    rename "^modid.accesswidener\$", "${project.mod_id}.accesswidener"
    rename "^modid.mixins.json\$", "${project.mod_id}.mixins.json"
}

loom {
    accessWidenerPath = file("src/main/resources/modid.accesswidener")

    runs {
        // This adds a new gradle task that runs the datagen API: "gradlew runDatagenClient"
        datagenClient {
            inherit client
            name "Minecraft Datagen"
            vmArg "-Dfabric-api.datagen"
            vmArg "-Dfabric-api.datagen.modid=${project.mod_id}"
            vmArg "-Dfabric-api.datagen.output-dir=${file("src/generated/resources")}"
            vmArg "-Dfabric-api.datagen.manual-dir=${file("src/main/resources")}"

            ideConfigGenerated = true
            runDir "build/datagen"
        }
    }

    mixin {
        defaultRefmapName = "${mod_id}.mixins.refmap.json"
    }
}

tasks.withType(JavaCompile).configureEach {
    // Minecraft 1.18 (1.18-pre2) upwards uses Java 17.
    it.options.release = Integer.parseInt((String) project.java_target)
}

java {
    // Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
    // if it is present.
    // If you remove this line, sources will not be generated.
    withSourcesJar()
    withJavadocJar()
}

// CurseGradle settings for uploading to CurseForge
curseforge {
    project {
        // Get the CurseForge token from the environment variables
        apiKey = System.getenv("CURSEFORGE_TOKEN")
        // Copy settings from gradle.properties
        id = project.curseforge_project_id
        releaseType = project.curseforge_release_type
        project.curseforge_game_versions.split(" ").each it::addGameVersion
        addGameVersion("Fabric")
        addGameVersion("Quilt")
        // Point to 'changelog' setting to a the changelog file
        changelog = file("changelog.md")
        changelogType = "markdown"
        if (!project.curseforge_required_dependency_ids.isEmpty() || !project.curseforge_optional_dependency_ids.isEmpty()) {
            relations {
                // Required dependencies
                if (!project.curseforge_required_dependency_ids.isEmpty())
                    project.curseforge_required_dependency_ids.split(" ").each it::requiredDependency
                // Optional dependencies
                if (!project.curseforge_optional_dependency_ids.isEmpty())
                    project.curseforge_optional_dependency_ids.split(" ").each it::optionalDependency
            }
        }

        mainArtifact(remapJar) {
            displayName = "${project.mod_name} ${project.mod_version} for Fabric ${project.minecraft_suffix.substring(2)}"
        }
        addArtifact(javadocJar) {
            it.displayName = "${project.mod_name} ${project.mod_version} for Forge ${project.minecraft_suffix.substring(2)} Javadoc"
        }
        addArtifact(sourcesJar) {
            it.displayName = "${project.mod_name} ${project.mod_version} for Forge ${project.minecraft_suffix.substring(2)} Sources"
        }
    }
    options {
        // Don't add project Java version
        javaVersionAutoDetect = false
    }
}

// Move CurseGradle's tasks from 'upload' to 'publishing'
project.gradle.taskGraph.whenReady {
    tasks.stream().filter(task -> task.group.equals "upload").each { task -> task.group = "publishing" }
}

// Minotaur settings for uploading to Modrinth
modrinth {
    // Get the Modrinth token from the environment variables
    token = System.getenv("MODRINTH_TOKEN")
    // Copy settings from gradle.properties
    projectId = project.modrinth_project_id
    versionNumber = version
    versionType = project.modrinth_release_type // Can be either 'release', 'alpha', or 'beta'
    versionName = "${project.mod_name} ${project.mod_version}"
    gameVersions = project.modrinth_game_versions.split(" ") as List
    // Convert the changelog file to text
    changelog = file("changelog.md").text
    uploadFile = remapJar // With Loom, this MUST be set to `remapJar` instead of `jar`!
    additionalFiles = [sourcesJar] // TODO add back javadocJar, once Modrinth actually accepts them
    loaders = ["fabric", "quilt"]
    if (!project.modrinth_required_dependency_ids.isEmpty() || !project.modrinth_optional_dependency_ids.isEmpty()) {
        dependencies {
            // Required dependencies
            if (!project.modrinth_required_dependency_ids.isEmpty())
                project.modrinth_required_dependency_ids.split(" ").each required::project
            // Optional dependencies
            if (!project.modrinth_optional_dependency_ids.isEmpty())
                project.modrinth_optional_dependency_ids.split(" ").each optional::project
        }
    }
}

// Create a task to upload to both CurseForge and Modrinth
task publishAll {
    group "publishing"
    dependsOn("curseforge", "modrinth")
    // Also build the file so I have a copy locally
    dependsOn "build"
}
