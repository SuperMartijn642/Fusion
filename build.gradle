buildscript {
    repositories {
        maven { url = "https://files.minecraftforge.net/maven" }
        mavenCentral()
    }
    dependencies {
        classpath group: "net.minecraftforge.gradle", name: "ForgeGradle", version: "5.1.+", changing: true
        classpath "org.spongepowered:mixingradle:0.7.+"
    }
}
plugins {
    id "io.github.CDAGaming.cursegradle" version "1.6.1"
    id "com.modrinth.minotaur" version "2.+"
}
apply plugin: "net.minecraftforge.gradle"
apply plugin: "eclipse"
apply plugin: "org.spongepowered.mixin"

version = project.mod_version + "-forge-" + project.minecraft_suffix
group = project.maven_group
archivesBaseName = project.mod_id

java.toolchain.languageVersion = JavaLanguageVersion.of(project.java_target)

println("Java: " + System.getProperty("java.version") + " JVM: " + System.getProperty("java.vm.version") + "(" + System.getProperty("java.vendor") + ") Arch: " + System.getProperty("os.arch"))

// Keep parameter names when compiling
compileJava.options.compilerArgs.add '-parameters'

repositories {
    flatDir {
        dirs "libs"
    }
    maven {
        url "https://www.cursemaven.com"
    }
    maven {
        url = "https://repo.spongepowered.org/maven"
    }
}

dependencies {
    // Forge
    minecraft "net.minecraftforge:forge:${project.minecraft_version}-${project.forge_version}"
    // SpongePowered Mixin
    annotationProcessor "org.spongepowered:mixin:0.8.5:processor"

    // Core library
    implementation fg.deobf("curse.maven:supermartijn642s-core-lib-454372:${project.core_library_file}")

    // VintageFix
    compileOnly fg.deobf("curse.maven:vintagefix-871198:4714078")
    // MixinBooter
//    runtimeOnly "curse.maven:mixin-booter-419286:4459218"

    // Just Enough Items
//    runtimeOnly fg.deobf("curse.maven:jei-238222:${project.just_enough_items_file}")

    // SpongePowered Mixin
    implementation("org.spongepowered:mixin:0.8.3-SNAPSHOT") {
        exclude module: "guava"
        exclude module: "commons-io"
        exclude module: "gson"
    }
}

// Include resources generated by data generators.
sourceSets.main {
    resources.srcDir "src/generated/resources"
    // Make sure resources are available for 1.12
    output.setResourcesDir output.classesDirs.asPath
}

processResources {
    inputs.property "version", project.version

    filesMatching(["mcmod.info", "modid.mixins.json", "pack.mcmeta"]) {
        expand project.properties
    }

    exclude "**/*.pdn"

    rename "^modid.mixins.json\$", "${project.mod_id}.mixins.json"
    rename "^icon.png\$", "${project.mod_id}.png"
    rename "^accesstransformer.cfg\$", "${project.mod_id}-accesstransformer.cfg"
}

minecraft {
    mappings channel: project.mappings_channel, version: project.mappings_version

    accessTransformer = file("src/main/resources/META-INF/accesstransformer.cfg")

    String resolved_core_plugin = "${project.mod_package}.${project.coremod_package}.${project.coremod_class}"
    String resolved_core_config = "${project.mod_id}.mixins.json"

    // Make sure the core lib's mixins get applied
    String core_lib_core_mod = "com.supermartijn642.core.coremod.CoreModPlugin"

    runs {
        client {
            workingDirectory project.file("run")

            property "forge.logging.markers", "REGISTRIES"
            property "fml.coreMods.load", resolved_core_plugin + "," + core_lib_core_mod + ",org.embeddedt.vintagefix.core.VintageFixCore,zone.rong.mixinbooter.MixinBooterPlugin"

            args "--mixin", resolved_core_config

            mods {
                source sourceSets.main
            }
        }

        server {
            workingDirectory project.file("run")

            property "forge.logging.markers", "REGISTRIES"
            property "fml.coreMods.load", resolved_core_plugin + "," + core_lib_core_mod

            args "--mixin", resolved_core_config

            mods {
                source sourceSets.main
            }
        }

        data {
            parent runs.client
            property "--generatorModid", project.mod_id
            property "--output", file("src/generated/resources/")
            property "--existing", file("$buildDir/data_resources")
        }
    }
}

task processSources(type: Sync) {
    from sourceSets.main.java
    into "$buildDir/sources"
    inputs.property 'version', version

    // Replace mod properties in main mod class
    Hashtable<String, String> properties = new Hashtable<>()
    project.properties.entrySet().stream()
            .filter(entry -> entry.key instanceof String && entry.value instanceof String)
            .forEach(entry -> properties.put(entry.key, entry.value))
    filesMatching(project.mod_package.replace('.', '/') + "/" + project.mod_main_class + ".java") {
        //noinspection UnnecessaryQualifiedReference
        filter(org.apache.tools.ant.filters.ReplaceTokens, tokens: properties)
    }
}

compileJava {
    source = processSources.outputs
}

jar {
    from {
        configurations.compileClasspath
                .filter { "mixin-0.8.3-SNAPSHOT.jar" == it.name }
                .collect { it.isDirectory() ? it : zipTree(it) }
    } {
        exclude "LICENSE.txt", "META-INF/MANIFSET.MF", "META-INF/maven/**", "META-INF/*.RSA", "META-INF/*.SF"
    }

    manifest {
        attributes "FMLCorePlugin": "${project.mod_package}.${project.coremod_package}.${project.coremod_class}"
        attributes "FMLCorePluginContainsFMLMod": "true"
        attributes "ForceLoadAsMod": "true"
        attributes "TweakClass": "org.spongepowered.asm.launch.MixinTweaker"
        attributes "TweakOrder": "0"
        attributes "MixinConfigs": "${project.mod_id}.mixins.json"
    }
}

//jar.finalizedBy('reobfJar')

mixin {
    // MixinGradle Settings
    add sourceSets.main, "${project.mod_id}.mixins.refmap.json"
    config "${project.mod_id}.mixins.json"

    debug.verbose = true
    debug.export = true
}

task prepareDataResources(type: Sync) {
    from file("src/main/resources")
    into "$buildDir/data_resources"
    inputs.property "version", version

    filesMatching(["mcmod.info", "modid.mixins.json", "pack.mcmeta"]) {
        expand project.properties
    }

    exclude "**/*.pdn"

    rename "^modid.mixins.json\$", "${project.mod_id}.mixins.json"
    rename "^icon.png\$", "${project.mod_id}.png"
    rename "^accesstransformer.cfg\$", "${project.mod_id}-accesstransformer.cfg"
}

// Run prepareDataResources before prepareRunData
tasks.whenTaskAdded {
    if (it.name == "prepareRunData")
        it.dependsOn "prepareDataResources"
}

tasks.register('javadocJar', Jar) {
    dependsOn(javadoc)
    group = "documentation"
    classifier = "javadoc"
    from(javadoc.destinationDir)
}

tasks.register('sourcesJar', Jar) {
    dependsOn(processResources, processSources)
    group = "documentation"
    classifier = "sources"
    from(processResources.destinationDir, processSources.destinationDir as Object)
}

// CurseGradle settings for uploading to CurseForge
curseforge {
    project {
        // Get the CurseForge token from the environment variables
        apiKey = System.getenv("CURSEFORGE_TOKEN")
        // Copy settings from gradle.properties
        id = project.curseforge_project_id
        releaseType = project.curseforge_release_type
        project.curseforge_game_versions.split(" ").each it::addGameVersion
        addGameVersion("Forge")
        // Point to 'changelog' setting to a the changelog file
        changelog = file("changelog.md")
        changelogType = "markdown"
        if (!project.curseforge_required_dependency_ids.isEmpty() || !project.curseforge_optional_dependency_ids.isEmpty()) {
            relations {
                // Required dependencies
                if (!project.curseforge_required_dependency_ids.isEmpty())
                    project.curseforge_required_dependency_ids.split(" ").each it::requiredDependency
                // Optional dependencies
                if (!project.curseforge_optional_dependency_ids.isEmpty())
                    project.curseforge_optional_dependency_ids.split(" ").each it::optionalDependency
            }
        }

        mainArtifact(jar) {
            it.displayName = "${project.mod_name} ${project.mod_version} for Forge ${project.minecraft_suffix.substring(2)}"
        }
        addArtifact(javadocJar) {
            it.displayName = "${project.mod_name} ${project.mod_version} for Forge ${project.minecraft_suffix.substring(2)}"
        }
        addArtifact(sourcesJar) {
            it.displayName = "${project.mod_name} ${project.mod_version} for Forge ${project.minecraft_suffix.substring(2)}"
        }
    }
    options {
        // Don't add project Java version
        javaVersionAutoDetect = false
    }
}

// Move CurseGradle's tasks from 'upload' to 'publishing'
project.gradle.taskGraph.whenReady {
    tasks.stream().filter(task -> task.group.equals "upload").each { task -> task.group = "publishing" }
}

// Minotaur settings for uploading to Modrinth
modrinth {
    // Get the Modrinth token from the environment variables
    token = System.getenv("MODRINTH_TOKEN")
    // Copy settings from gradle.properties
    projectId = project.modrinth_project_id
    versionNumber = version
    versionType = project.modrinth_release_type // Can be either 'release', 'alpha', or 'beta'
    versionName = "${project.mod_name} ${project.mod_version}"
    gameVersions = project.modrinth_game_versions.split(" ") as List
    // Convert the changelog file to text
    changelog = file("changelog.md").text
    uploadFile = jar // With Loom, this MUST be set to `remapJar` instead of `jar`!
    additionalFiles = [javadocJar, sourcesJar]
    loaders = ["forge"]
    if (!project.modrinth_required_dependency_ids.isEmpty() || !project.modrinth_optional_dependency_ids.isEmpty()) {
        dependencies {
            // Required dependencies
            if (!project.modrinth_required_dependency_ids.isEmpty())
                project.modrinth_required_dependency_ids.split(" ").each required::project
            // Optional dependencies
            if (!project.modrinth_optional_dependency_ids.isEmpty())
                project.modrinth_optional_dependency_ids.split(" ").each optional::project
        }
    }
}

// Create a task to upload to both CurseForge and Modrinth
task publishAll {
    group "publishing"
    dependsOn("curseforge", "modrinth")
    // Also build the file so I have a copy locally
    dependsOn "build"
}
