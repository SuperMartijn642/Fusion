plugins {
    id "eclipse"
    id "idea"
    id "java-library"
    id "net.neoforged.gradle.userdev" version "7.0.57"
    id "io.github.CDAGaming.cursegradle" version "1.6.1"
    id "com.modrinth.minotaur" version "2.+"
}

version = project.mod_version + "-neoforge-" + project.minecraft_suffix
group = project.maven_group
base.archivesName = project.mod_id

java.toolchain.languageVersion = JavaLanguageVersion.of(project.java_target)

println("Java: " + System.getProperty("java.version") + " JVM: " + System.getProperty("java.vm.version") + "(" + System.getProperty("java.vendor") + ") Arch: " + System.getProperty("os.arch"))

// Keep parameter names when compiling
compileJava.options.compilerArgs.add '-parameters'

repositories {
    flatDir {
        dirs "libs"
    }
    maven {
        url "https://www.cursemaven.com"
    }
    exclusiveContent {
        forRepository {
            maven {
                name = "Modrinth"
                url = "https://api.modrinth.com/maven"
            }
        }
        filter {
            includeGroup "maven.modrinth"
        }
    }
}

dependencies {
    // NeoForge
    implementation "net.neoforged:neoforge:${project.neoforge_version}"

    // Just Enough Items
//    runtimeOnly "curse.maven:jei-238222:${project.just_enough_items_file}"
}

// Include resources generated by data generators.
sourceSets.main.resources { srcDir "src/generated/resources" }

sourceSets.each {
    def dir = layout.buildDirectory.dir("sourcesSets/$it.name")
    it.output.resourcesDir = dir
    it.java.destinationDirectory = dir
}

tasks.withType(ProcessResources).configureEach {
    inputs.property "version", project.version

    filesMatching(["META-INF/mods.toml", "modid.mixins.json", "pack.mcmeta"]) {
        expand project.properties
    }

    exclude "**/*.pdn"

    rename "^modid.mixins.json\$", "${project.mod_id}.mixins.json"
    rename "^icon.png\$", "${project.mod_id}.png"
}

minecraft.accessTransformers.file rootProject.file("src/main/resources/META-INF/accesstransformer.cfg")

runs {
    configureEach {
        systemProperty "forge.logging.markers", "REGISTRIES"

        modSource project.sourceSets.main
    }
    client {
    }
    server {
        programArgument "-nogui"
    }
    data {
        programArguments.addAll "--mod", project.mod_id, "--all", "--output", file("src/generated/resources/").getAbsolutePath(), "--existing", file("src/main/resources").getAbsolutePath()
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8' // Use the UTF-8 charset for Java compilation
}

tasks.register('javadocJar', Jar) {
    dependsOn javadoc
    group = "documentation"
    archiveClassifier = "javadoc"
    from javadoc.destinationDir
}

tasks.register('sourcesJar', Jar) {
    dependsOn classes
    group = "documentation"
    archiveClassifier = "sources"
    from sourceSets.main.allSource
}

// Remove the 'idePostSync' task from running after sync and run it in the gradle script directly to not break when switching to other branches (https://youtrack.jetbrains.com/issue/IDEA-282322/Gradle-Execute-After-Sync-keeps-trying-to-run-a-removed-task)
idea.project.settings.taskTriggers.phaseMap.get("afterSync").removeIf(task -> task.name.equals("idePostSync"))

// CurseGradle settings for uploading to CurseForge
curseforge {
    project {
        // Get the CurseForge token from the environment variables
        apiKey = System.getenv("CURSEFORGE_TOKEN")
        // Copy settings from gradle.properties
        id = project.curseforge_project_id
        releaseType = project.curseforge_release_type
        project.curseforge_game_versions.split(" ").each it::addGameVersion
        addGameVersion("NeoForge")
        // Point to 'changelog' setting to a the changelog file
        changelog = file("changelog.md")
        changelogType = "markdown"
        if (!project.curseforge_required_dependency_ids.isEmpty() || !project.curseforge_optional_dependency_ids.isEmpty()) {
            relations {
                // Required dependencies
                if (!project.curseforge_required_dependency_ids.isEmpty())
                    project.curseforge_required_dependency_ids.split(" ").each it::requiredDependency
                // Optional dependencies
                if (!project.curseforge_optional_dependency_ids.isEmpty())
                    project.curseforge_optional_dependency_ids.split(" ").each it::optionalDependency
            }
        }

        mainArtifact(jar) {
            it.displayName = "${project.mod_name} ${project.mod_version} for NeoForge ${project.minecraft_suffix.substring(2)}"
        }
        addArtifact(javadocJar) {
            it.displayName = "${project.mod_name} ${project.mod_version} for NeoForge ${project.minecraft_suffix.substring(2)}"
        }
        addArtifact(sourcesJar) {
            it.displayName = "${project.mod_name} ${project.mod_version} for NeoForge ${project.minecraft_suffix.substring(2)}"
        }
    }
    options {
        // Don't add project Java version
        javaVersionAutoDetect = false
    }
}

// Move CurseGradle's tasks from 'upload' to 'publishing'
project.gradle.taskGraph.whenReady {
    tasks.stream().filter(task -> task.group.equals "upload").each { task -> task.group = "publishing" }
}

// Minotaur settings for uploading to Modrinth
modrinth {
    // Get the Modrinth token from the environment variables
    token = System.getenv("MODRINTH_TOKEN")
    // Copy settings from gradle.properties
    projectId = project.modrinth_project_id
    versionNumber = version
    versionType = project.modrinth_release_type // Can be either 'release', 'alpha', or 'beta'
    versionName = "${project.mod_name} ${project.mod_version}"
    gameVersions = project.modrinth_game_versions.split(" ") as List
    // Convert the changelog file to text
    changelog = file("changelog.md").text
    uploadFile = jar // With Loom, this MUST be set to `remapJar` instead of `jar`!
    additionalFiles = [javadocJar, sourcesJar]
    loaders = ["neoforge"]
    if (!project.modrinth_required_dependency_ids.isEmpty() || !project.modrinth_optional_dependency_ids.isEmpty()) {
        dependencies {
            // Required dependencies
            if (!project.modrinth_required_dependency_ids.isEmpty())
                project.modrinth_required_dependency_ids.split(" ").each required::project
            // Optional dependencies
            if (!project.modrinth_optional_dependency_ids.isEmpty())
                project.modrinth_optional_dependency_ids.split(" ").each optional::project
        }
    }
}

// Create a task to upload to both CurseForge and Modrinth
task publishAll {
    group "publishing"
    dependsOn("curseforge", "modrinth")
    // Also build the file so I have a copy locally
    dependsOn "build"
}
